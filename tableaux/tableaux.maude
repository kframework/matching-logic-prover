fmod MATCHING-LOGIC-PATTERN is
    protecting NAT .

    sort Symbol .
    ops a b c : -> Symbol .

    --- SetVariable ---
    -------------------

    sort SetVariable .
    ops X Y Z : -> SetVariable .

    sort IndexedSetVariable .
    op _ { _ } : SetVariable Nat -> IndexedSetVariable .

    --- We use special constructors for variables for DefinitionConstants
    ops u : Nat -> SetVariable .
    ops v : Nat -> SetVariable .

    --- Pattern ---
    ---------------

    sort Pattern .

    subsort Symbol IndexedSetVariable < Pattern .
    op \top : -> Pattern .
    op \bottom : -> Pattern .
    op \not _ : Symbol -> Pattern .

    --- We use Modal Mu Calculus application syntax for now.
    op < _ > _ : Symbol Pattern -> Pattern .
    op [ _ ] _ : Symbol Pattern -> Pattern .

    op \and : Pattern Pattern -> Pattern [assoc comm id: \top] .
    op \or  : Pattern Pattern -> Pattern [assoc comm id: \bottom] .

    --- TODO: Plug in CINNI binders?
    op \mu _ . _ : SetVariable Pattern -> Pattern .
    op \nu _ . _ : SetVariable Pattern -> Pattern .

endfm

view Pattern from TRIV to MATCHING-LOGIC-PATTERN is
    sort Elt to Pattern .
endv

view SetVariable from TRIV to MATCHING-LOGIC-PATTERN is
    sort Elt to SetVariable .
endv

fmod MATCHING-LOGIC-PATTERN-SET is
    protecting MATCHING-LOGIC-PATTERN .

    including SET{Pattern}
            * (sort NeSet{Pattern} to NePatternSet, sort Set{Pattern} to PatternSet) .
endfm

--- TODO: Should we move this to CINNI?
fmod MATCHING-LOGIC-SUBSTITUTION is
    protecting MATCHING-LOGIC-PATTERN-SET .

    vars SVar SVar1 SVar2 : SetVariable .
    vars I : Nat .
    vars P P1 P2 : Pattern .
    vars Sy Sy1 Sy2 : Symbol .
    vars Subst : Subst .

    sort Subst .
    op _ _ : Subst Pattern -> Pattern .

    op [_ := _]     : SetVariable Pattern -> Subst .
    op [shift _ ]   : SetVariable         -> Subst .
    op [lift _ _ ]  : SetVariable Subst   -> Subst .
   -------------------------------------------------

    eq [SVar1 := P] SVar1 { 0 }    = P .
    eq [SVar1 := P] SVar1 { s(I) } = SVar1 { I } .
   ceq [SVar1 := P] SVar2 { I }    = SVar2 { I } if SVar1 =/= SVar2 .

    eq [shift SVar1] SVar1 { I } = SVar1 { I + 1 } .
   ceq [shift SVar1] SVar2 { I } = SVar2 { I }   if SVar1 =/= SVar2 .

    eq [lift SVar Subst] SVar  { 0 }    = SVar { 0 } .
    eq [lift SVar Subst] SVar  { s(I) } = [shift SVar] Subst SVar { I } .
   ceq [lift SVar Subst] SVar1 { I }    = [shift SVar] Subst SVar1 { I } if SVar =/= SVar1 .

    eq Subst Sy = Sy .
    eq Subst \not Sy = \not (Subst Sy) .
    eq Subst < Sy > P1 = < Subst Sy > Subst P1 .
    eq Subst [ Sy ] P1 = [ Subst Sy ] Subst P1 .
   ceq Subst \and(P1, P2) = \and(Subst P1, Subst P2) if P1 =/= \top    /\ P2 =/= \top    .
   ceq Subst \or (P1, P2) = \or (Subst P1, Subst P2) if P1 =/= \bottom /\ P2 =/= \bottom .

    eq Subst \mu SVar1 . P1 = \mu SVar1 . [lift SVar1 Subst] P1 .
    eq Subst \nu SVar1 . P1 = \nu SVar1 . [lift SVar1 Subst] P1 .

    op * : -> SetVariable .
   ------------------------
   ceq \mu SVar1 . P1 = \mu * . [SVar1 := *{0} ][shift * ] P1 if SVar1 =/= * .
   ceq \nu SVar1 . P1 = \nu * . [SVar1 := *{0} ][shift * ] P1 if SVar1 =/= * .

endfm

--- TODO: Can we have DefinitionSet without resorting to a special module?
fmod MATCHING-LOGIC-TABLEAUX-DEFINITION-CONSTANT is
    protecting MATCHING-LOGIC-PATTERN-SET .
    sort DefinitionConstant .
    op ( _ := _ ) : SetVariable Pattern -> DefinitionConstant .
endfm

view DefinitionConstant from TRIV to MATCHING-LOGIC-TABLEAUX-DEFINITION-CONSTANT is
    sort Elt to DefinitionConstant .
endv

fmod MATCHING-LOGIC-TABLEAUX-DEFINITION-LIST is
    protecting MATCHING-LOGIC-TABLEAUX-DEFINITION-CONSTANT .
    protecting MATCHING-LOGIC-PATTERN-SET .
    protecting LIST{DefinitionConstant}
             * ( sort NeList{DefinitionConstant} to NeDefinitionList
               , sort List{DefinitionConstant}   to DefinitionList
               ) .

    vars SVar SVar1 SVar2 : SetVariable .
    vars P P1 P2 : Pattern .
    vars Sy Sy1 Sy2 : Symbol .
    var DefList : DefinitionList .

    op _ in image(_) : Pattern DefinitionList -> Bool .
   ----------------------------------------------------
    eq P in image(nil) = false .
    eq P in image((SVar := P) DefList) = true .
   ceq P1 in image((SVar := P2) DefList) = P1 in image(DefList) if P1 =/= P2 .

    op _ in domain(_) : SetVariable DefinitionList -> Bool .
   ----------------------------------------------------
    eq SVar in domain((SVar := P) DefList) = true .
    eq SVar in domain(            DefList) = false [owise] .

    op reverseLookup : Pattern DefinitionList -> [IndexedSetVariable] .
   -------------------------------------------------------------
    eq reverseLookup(P, (SVar := P ) DefList) = SVar {0} .
   ceq reverseLookup(P, (SVar := P1) DefList) = reverseLookup(P, DefList) if P =/= P1 .

endfm

fmod MATCHING-LOGIC-TABLEAUX-CONTRACT is
    protecting MATCHING-LOGIC-TABLEAUX-DEFINITION-LIST .
    protecting MATCHING-LOGIC-SUBSTITUTION .

    vars SVar SVar1 SVar2 : SetVariable .
    vars P P1 P2 : Pattern .
    vars Sy Sy1 Sy2 : Symbol .
    vars I : Nat .

    op contract : Pattern -> DefinitionList .
   ------------------------------------------
    eq contract(P) = contract(nil, 0, P) .

    var Rank : Nat .
    var DefList : DefinitionList .

    op contract : DefinitionList Nat Pattern -> DefinitionList .
   -------------------------------------------------------------
    eq contract(DefList, Rank, Sy)           = nil .
    eq contract(DefList, Rank, SVar { I })   = nil .
    eq contract(DefList, Rank, \not Sy)      = nil .
    eq contract(DefList, Rank, < Sy > P)     = contract(DefList, Rank, P) .
    eq contract(DefList, Rank, [ Sy ] P)     = contract(DefList, Rank, P) .
   ceq contract(DefList, Rank, \and(P1, P2)) = merge(contract(DefList, Rank, P1), Rank, P2) if P1 =/= \top    /\ P2 =/= \top .
   ceq contract(DefList, Rank, \or (P1, P2)) = merge(contract(DefList, Rank, P1), Rank, P2) if P1 =/= \bottom /\ P2 =/= \bottom .

   ceq contract(DefList, Rank, \mu SVar . P) = (u(Rank) := \mu SVar . P)
                                               contract(DefList (u(Rank) := \mu SVar . P),  Rank + 1, [SVar := u(Rank) {0}] P)
    if not \mu SVar . P in image(DefList) .
   ceq contract(DefList, Rank, \mu SVar . P) = contract(DefList,  Rank + 1, [SVar := reverseLookup(\mu SVar . P, DefList)] P)
    if \mu SVar . P in image(DefList) .

   ceq contract(DefList, Rank, \nu SVar . P) = (v(Rank) := \nu SVar . P)
                                               contract(DefList (v(Rank) := \nu SVar . P),  Rank + 1, [SVar := v(Rank) {0}] P)
    if not \nu SVar . P in image(DefList) .
   ceq contract(DefList, Rank, \nu SVar . P) = contract(DefList,  Rank + 1, [SVar := reverseLookup(\nu SVar . P, DefList)] P)
    if \nu SVar . P in image(DefList) .

    op merge : DefinitionList Nat Pattern -> DefinitionList .
   ----------------------------------------------------------
    eq merge(DefList, Rank, P) = append(DefList, contract(DefList, Rank + size(DefList), P)) .

endfm

mod MATCHING-LOGIC-TABLEAUX is
    protecting MATCHING-LOGIC-TABLEAUX-CONTRACT .

    sort Sequent .

    var DefList DefList1 DefList2 : DefinitionList .
    vars P P1 P2 : Pattern .
    vars Gamma Ps P1s P2s : PatternSet .
    vars SVar SVar1 SVar2 : SetVariable .
    vars Sy Sy1 Sy2 : Symbol .
    vars I : Nat .

    op tableaux : Pattern -> Sequent .
    -----------------------------------
    eq tableaux(P) = [ P ; contract(P) ] .

    --- Is Inconsistent? ----
    -------------------------

    --- The various tableaux rules apply only if Gamma is consistent.
    --- i.e. if there is no constant c and its negation in Gamma.
    --- We implement this via the special sequent `[_;_]`.
    --- This sequent equals an ordinary sequent only if Gamma is consistent.

    op [_;_] : PatternSet DefinitionList -> Sequent .
   --------------------------------------------------
   ceq [ Gamma ; DefList ] = < Gamma ; DefList >
    if isConsistent(Gamma)
     .

    op isConsistent(_) : PatternSet -> Bool .
    --------------------------------------
    eq isConsistent(empty)               = true .
    eq isConsistent(Sy, \not Sy,  Gamma) = false .
   ceq isConsistent(     Sy,      Gamma) = isConsistent(Gamma) if not (\not Sy) in Gamma .
   ceq isConsistent(\not Sy,      Gamma) = isConsistent(Gamma) if not (     Sy) in Gamma .
    eq isConsistent(\and(P1, P2), Gamma) = isConsistent(Gamma) .
    eq isConsistent(\or (P1, P2), Gamma) = isConsistent(Gamma) .
    eq isConsistent(\mu SVar . P, Gamma) = isConsistent(Gamma) .
    eq isConsistent(\nu SVar . P, Gamma) = isConsistent(Gamma) .
    eq isConsistent(SVar {I},     Gamma) = isConsistent(Gamma) .

    ---- Tableaux Rules -----
    -------------------------

    op <_;_> : PatternSet DefinitionList -> Sequent .


   ceq [and] : < \and(P1, P2), Gamma ; DefList >
             = ---------------------------------
               [ P1, P2, Gamma ; DefList ]
        if P1 =/= \top /\ P2 =/= \top
      .

   crl [or-l] : < \or(P1, P2), Gamma ; DefList >
             => --------------------------------
                [ P1, Gamma ; DefList ]
       if P1 =/= \bottom /\ P2 =/= \bottom
     .
   crl [or-r] : < \or(P1, P2), Gamma ; DefList >
             => --------------------------------------
                [ P2, Gamma ; DefList ]
       if P1 =/= \bottom /\ P2 =/= \bottom
     .

    rl [ons-mu] : < SVar1 {0}, Gamma ; DefList1 (SVar1 := \mu SVar2 . P) DefList2 >
               => -----------------------------------------------------------------------------
                  [ [SVar2 := SVar1 {0}]P, Gamma ; DefList1 (SVar1 := \mu SVar2 . P) DefList2 ]
               .
    rl [ons-nu] : < SVar1 {0}, Gamma ; DefList1 (SVar1 := \nu SVar2 . P) DefList2 >
              => -------------------------------------------------------------------------
                 [ [SVar2 := SVar1 {0}]P, Gamma ; DefList1 (SVar1 := \nu SVar2 . P) DefList2 ]
               .

    eq [nu/mu] : < P, Gamma         ; DefList1 (SVar1 := P) DefList2 >
              = -------------------------------------------------------------------
                 [ SVar1 {0}, Gamma ; DefList1 (SVar1 := P) DefList2 ]
       .

   crl [all<>] : < < Sy > P , Gamma ; DefList >
              => -------------------------------------
                 [ P, getBetas(Sy ; Gamma) ; DefList ]
       if isReduced(Gamma ; DefList)
     .

    op isReduced(_ ; _) : PatternSet DefinitionList -> Bool .
    eq isReduced(empty ; DefList) = true .
   ceq isReduced(\and(P1, P2), Gamma ; DefList) = false if P1 =/= \top /\ P2 =/= \top .
   ceq isReduced(\or (P1, P2), Gamma ; DefList) = false if P1 =/= \bottom /\ P2 =/= \bottom .
    eq isReduced(\mu SVar1 . P, Gamma ; DefList) = false .
    eq isReduced(\nu SVar1 . P, Gamma ; DefList) = false .
    eq isReduced(< Sy > P, Gamma ; DefList) = isReduced(Gamma ; DefList) .
    eq isReduced([ Sy ] P, Gamma ; DefList) = isReduced(Gamma ; DefList) .
    eq isReduced(Sy, Gamma ; DefList) = isReduced(Gamma ; DefList) .
   ceq isReduced(SVar {I}, Gamma ; DefList) = isReduced(Gamma ; DefList) if not SVar in domain(DefList) .
    eq isReduced(SVar {0}, Gamma ; (SVar := P) DefList) = false .
    eq isReduced(\not Sy, Gamma ; DefList) = isReduced(Gamma ; DefList) .

    op getBetas(_;_) : Symbol PatternSet -> PatternSet .
    eq getBetas(Sy ; empty ) = empty .
    eq getBetas(Sy ; [ Sy ] P , Gamma ) = P, getBetas(Sy ; Gamma ) .
    eq getBetas(Sy ; P , Gamma ) = getBetas(Sy ; Gamma ) [owise] .

endm
